<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:c="ui.components.*"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         xmlns:p="ui.panes.*"
         creationComplete="onCreationComplete(event)">

    <fx:Script>
        <![CDATA[
        import com.greensock.TweenLite;

        import game.controllers.Tectonics;

        import game.graph.Cell;

        import game.task.Task;
        import game.task.TaskId;

        import global.Color;
        import global.Icons;
        import global.Local;
        import global.Util;

        import managers.PopupManager;
        import managers.State;

        import mx.events.FlexEvent;

        import ui.popups.PopupBusy;
        import ui.popups.PopupConfirm;
        import ui.popups.PopupInfo;
        import ui.popups.PopupTasks;
        import ui.toolbars.Toolbar;
        import ui.toolbars.ToolbarTectonicPlates;

        private var map:Map;
        private var toolbar:Toolbar;

        private var isDragging:Boolean = false;

        private function onCreationComplete(event:FlexEvent):void
        {
            if (!State.loaded)
                State.loadLocal();

            map = new Map(canvas);
            canvas.addEventListener(Canvas.ZOOM_CHANGED, onZoom);
            paneLayers.listLayers.dataProvider = map.layers;

            addEventListeners();

            validate();
        }

        private function onZoom(event:Event):void
        {
            validateZoom();
        }

        private function validateZoom():void
        {
            labelZoom.text = int(canvas.zoomLevel * 100) + "%";
        }

        private function addEventListeners():void
        {
            State.dispatcher.addEventListener(State.STATE_CHANGED, function (event:Event):void
            {
                validate();
            });

            systemManager.stage.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoveAnywhere);
            canvas.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoveOverCanvas);
            canvas.addEventListener(MouseEvent.CLICK, onMouseClickCanvas);
            canvas.addEventListener(MouseEvent.MOUSE_DOWN, onMouseDownCanvas);
            canvas.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMoveCanvas);
            canvas.addEventListener(MouseEvent.MOUSE_UP, onMouseUpCanvas);

            viewStackPanes.addEventListener(Event.CLOSE, onPaneClose);
        }

        private function onMouseDownCanvas(event:MouseEvent):void
        {
            isDragging = true;
        }

        private function onMouseMoveCanvas(event:MouseEvent):void
        {
            if (!isDragging)
                return;
        }

        private function onMouseUpCanvas(event:MouseEvent):void
        {
            isDragging = false;
        }

        private function onMouseClickCanvas(event:MouseEvent):void
        {
            var point:Point = Map.instance.getClosestPoint(new Point(canvas.mapLayerGroup.mouseX, canvas.mapLayerGroup.mouseY));
            if (!point)
                return;

            var cell:Cell = Map.instance.getCellbyPoint(point);

            // The current task determines the action
            switch (Task.current.id)
            {
                case TaskId.MAKE_TECTONIC_PLATES:
                    if (ToolbarTectonicPlates(toolbar).choosingPlateOriginId)
                    {
                        // Does this cell already have a plate origin assigned to it?
                        if (Tectonics.plateByOrigin(cell))
                        {
                            var p:PopupInfo = new PopupInfo();
                            p.header = Local.text('invalid_cell');
                            p.description = Local.text('plate_choose_new_origin');
                            PopupManager.open(p);

                            ToolbarTectonicPlates(toolbar).cancelChoosePlate();
                            break;
                        }

                        // Otherwise
                        Tectonics.setPlateOrigin(cell, ToolbarTectonicPlates(toolbar).choosingPlateOriginId);
                        ToolbarTectonicPlates(toolbar).confirmPlateOrigin();
                        Artist.drawTectonicPlates();
                    }
                    break;
                default:
                    break;
            }
        }

        private function onMouseMoveOverCanvas(event:MouseEvent):void
        {
            if (canvas.isDragging)
                return;

            // Get nearest point
            var nearestPoint:Point = map.getClosestPoint(new Point(canvas.mapLayerGroup.mouseX, canvas.mapLayerGroup.mouseY));
            labelNearestPoint.text = "";
            if (nearestPoint)
                labelNearestPoint.text = "x=" + nearestPoint.x + ", y=" + nearestPoint.y + "";
            groupNearestPoint.visible = nearestPoint != null;
        }

        private function onMouseMoveAnywhere(event:MouseEvent):void
        {
            labelMouse.text = "x=" + canvas.mapLayerGroup.mouseX + ", y=" + canvas.mapLayerGroup.mouseY;

            // Only hide nearest point
            var nearestPoint:Point = map.getClosestPoint(new Point(canvas.mapLayerGroup.mouseX, canvas.mapLayerGroup.mouseY));
            if (!nearestPoint)
                groupNearestPoint.visible = false;
        }

        private function onPaneClose(event:Event):void
        {
            viewStackPanes.visible = viewStackPanes.includeInLayout = false;
        }

        private function onToggleLayers(event:MouseEvent):void
        {
            // If it's already open, close it
            if (viewStackPanes.selectedChild == viewLayers && viewStackPanes.visible)
            {
                onPaneClose(null);
                return;
            }

            // Otherwise, show the layers view
            viewStackPanes.visible = viewStackPanes.includeInLayout = true;
            viewStackPanes.selectedChild = viewLayers;
        }

        private function validate():void
        {
            validateTask();
            paneLayers.validate();
            validateButtonNextTask();
            callLater(validateButtonApply);
            validateZoom();
        }

        private function validateButtonNextTask():void
        {
            var nextTaskIndex:int = Task.byId(State.read("task")).index + 1;
            if (nextTaskIndex >= Task.tasks.length)
                buttonNextTask.enabled = buttonNextTask.visible = buttonNextTask.includeInLayout = false;
        }

        private function validateTask():void
        {
            if (State.read("task") == undefined)
                State.write("task", TaskId.READ_INTRODUCTION);

            var taskId:String = State.read("task");
            var task:Task = Task.byId(State.read("task"));

            if (toolbar)
            {
                // Don't reload the toolbar if it's the same
                var s:String = toolbar.className;
                var t:String = getQualifiedClassName(task.toolbar);
                t = t.substr(t.indexOf("::") + "::".length);
                if (s == t)
                    return;
            }

            labelTaskNumber.text = (task.index + 1) + "/" + Task.tasks.length;
            labelTaskName.text = Local.text(Util.camelCaseToUnderScore(task.id));

            if (!toolbar || toolbar.taskId != taskId)
            {
                // Dispose existing toolbar
                if (toolbar)
                {
                    toolbar.dispose();
                    groupToolbar.removeElement(toolbar);
                }

                toolbar = new task.toolbar;
                toolbar.percentWidth = 100;
                toolbar.percentHeight = 100;
                groupToolbar.addElement(toolbar);
                scrollerToolbar.visible = false;

                setTimeout(function ():void
                {
                    scrollerToolbar.visible = true;
                    TweenLite.fromTo(scrollerToolbar, .5, {alpha: 0}, {
                        alpha: 1, onComplete: function ():void
                        {
                            var p:PopupBusy = new PopupBusy();
                            p.text = Local.text('loading');
                            p.calculation = function ():void
                            {
                                Map.instance.loadPreviousTasks(taskId);
                            };
                            p.callbackComplete = calculateWithPopup;
                            PopupManager.open(p);
                        }
                    });
                }, 500);
            }

            map.validateAllowedLayers();
        }

        private function onAllTasks(event:MouseEvent):void
        {
            var p:PopupTasks = new PopupTasks();
            PopupManager.open(p);
        }

        private function onApply(event:MouseEvent):void
        {
            calculateWithPopup();
        }

        private function calculateWithPopup():void
        {
            var p:PopupBusy = new PopupBusy();
            p.calculation = Map.instance.calculate;
            p.text = Local.text('calculating');
            p.callbackComplete = validateButtonApply;
            PopupManager.open(p);
        }

        private function validateButtonApply():void
        {
            var i:int = 0;
            for (var category:String in Map.instance.invalidatedOperations)
                i++;
            buttonApply.label = Local.text("apply") + (i > 0 ? " *" : "");
        }

        private function onNextTask(event:MouseEvent):void
        {
            var p:PopupConfirm = new PopupConfirm();
            p.header = Local.text("popup_complete_task_header"); // Complete this task
            p.description = Local.text("popup_complete_task_description"); // Completing this task will lock in your choices permanently and progress to the next task
            p.textConfirm = Local.text("yes_im_sure"); // Yes, I'm sure
            p.textCancel = Local.text("cancel"); // Cancel
            p.callbackConfirm = onNextTaskConfirm;

            PopupManager.open(p);
        }

        public function onNextTaskConfirm():void
        {
            // Go to the next task
            var newTaskIndex:int = Task.byId(State.read("task")).index + 1;
            var newTask:Task = Task.byIndex(newTaskIndex);
            State.write("task", newTask.id);
            validateButtonNextTask();
        }

        private function onZoomIn(event:MouseEvent):void
        {
            canvas.zoomIn();
        }

        private function onZoomOut(event:MouseEvent):void
        {
            canvas.zoomOut();
        }

        private function onCenterMap(event:MouseEvent):void
        {
            canvas.centerMap();
        }
        ]]>
    </fx:Script>

    <s:Group width="100%"
             height="100%">

        <s:HGroup width="100%"
                  height="100%"
                  padding="15"
                  gap="15">

            <mx:ViewStack id="viewStackPanes"
                          width="320"
                          height="100%"
                          visible="false"
                          includeInLayout="false">
                <s:NavigatorContent id="viewLayers"
                                    width="100%"
                                    height="100%">
                    <p:PaneLayers id="paneLayers"
                                  width="100%"
                                  height="100%"/>
                </s:NavigatorContent>
            </mx:ViewStack>

            <s:Group width="100%"
                     height="100%">

                <!--Canvas-->
                <c:Canvas id="canvas"
                          width="100%"
                          height="100%"/>

                <!--Map Controls-->
                <s:Group width="100%">
                    <s:Rect width="100%"
                            height="100%">
                        <s:fill>
                            <s:SolidColor color="{Color.black}"
                                          alpha=".8"/>
                        </s:fill>
                    </s:Rect>
                    <s:HGroup width="100%"
                              padding="5"
                              verticalAlign="middle">
                        <s:Button skinClass="ui.skins.ButtonSkinIcon"
                                  icon="{Icons.Layers}"
                                  toolTip="{Local.text('map_layers')}"
                                  click="onToggleLayers(event)"/>

                        <s:Spacer width="100%"/>

                        <s:Label id="labelMouse"/>

                        <s:Label id="labelZoom"/>
                        <s:Button skinClass="ui.skins.ButtonSkinIcon"
                                  icon="{Icons.Minus}"
                                  toolTip="{Local.text('zoom_out')}"
                                  click="onZoomOut(event)"/>
                        <s:Button skinClass="ui.skins.ButtonSkinIcon"
                                  icon="{Icons.Plus}"
                                  toolTip="{Local.text('zoom_in')}"
                                  click="onZoomIn(event)"/>
                        <s:Button skinClass="ui.skins.ButtonSkinIcon"
                                  icon="{Icons.Fit}"
                                  toolTip="{Local.text('center_map')}"
                                  click="onCenterMap(event)"/>

                    </s:HGroup>
                </s:Group>
                <s:HGroup id="groupNearestPoint"
                          width="100%"
                          height="100%"
                          horizontalAlign="center"
                          verticalAlign="bottom"
                          bottom="5"
                          visible="false"
                          mouseEnabled="false">
                    <s:Group>
                        <s:Rect width="100%"
                                height="100%"
                                radiusX="15"
                                radiusY="15">
                            <s:fill>
                                <s:SolidColor color="{Color.black}"
                                              alpha=".7"/>
                            </s:fill>
                        </s:Rect>
                        <s:HGroup verticalAlign="middle"
                                  padding="5"
                                  paddingRight="10">
                            <c:ColorBitmapImage id="iconNearestPoint"
                                                color="{Color.theme}"
                                                source="{Icons.Target}"/>
                            <s:Label id="labelNearestPoint"
                                     color="{Color.theme}"/>
                        </s:HGroup>
                    </s:Group>
                </s:HGroup>
            </s:Group>

            <!--Task Actions-->
            <s:Panel skinClass="ui.skins.PanelSkinOutline"
                     width="320"
                     height="100%">

                <s:VGroup width="100%"
                          height="100%"
                          padding="10"
                          gap="10">

                    <!--Task Actions-->
                    <s:HGroup width="100%"
                              verticalAlign="middle">

                        <s:Label id="labelTaskNumber"
                                 toolTip="{Local.text('task_progress')}"/>

                        <s:Label id="labelTaskName"
                                 showTruncationTip="true"
                                 width="100%"/>

                        <s:Button label="{Local.text('all_tasks')}"
                                  icon="{Icons.Tasks}"
                                  click="onAllTasks(event)"/>
                    </s:HGroup>

                    <s:Scroller id="scrollerToolbar"
                                width="100%"
                                height="100%"
                                verticalScrollPolicy="auto"
                                horizontalScrollPolicy="off">
                        <s:Group id="groupToolbar"
                                 width="100%"/>
                    </s:Scroller>

                    <s:HGroup width="100%"
                              horizontalAlign="center">
                        <s:Button id="buttonApply"
                                  label="{Local.text('apply')}"
                                  click="onApply(event)"/>
                        <s:Button id="buttonNextTask"
                                  skinClass="ui.skins.ButtonThemeSkin"
                                  label="{Local.text('next_task')}"
                                  click="onNextTask(event)"/>
                    </s:HGroup>

                </s:VGroup>

            </s:Panel>

        </s:HGroup>

    </s:Group>

</s:Group>
